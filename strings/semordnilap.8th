\ From RosettaCode: http://rosettacode.org/wiki/Semordnilap
\
\ A semordnilap is a word (or phrase) that spells a different word (or phrase)
\ backward. "Semordnilap" is a word that itself is a semordnilap.
\ 
\ Example: lager and regal
\
\ Using only words from the unixdict, report the total number of unique
\ semordnilap pairs, and print 5 examples. (Note that lager/regal and
\ regal/lager should be counted as one unique pair.)

[] var, results

: processline \ m s --
  clone nip
  tuck s:rev
  m:exists? if 
    results @ rot a:push drop
  else
    swap true m:!
  then ;

{} "unixdict.txt" app:asset >s
' processline s:eachline 

results @ a:len . " pairs" . cr
a:shuffle
( a:shift dup .  " is the reverse of " . s:rev . cr ) 5 times bye
