\ calculate PI using "big floating point"
\ The Nilakantha series :
\ Ï€ = 3 + 4/(2*3*4) - 4/(4*5*6) + 4/(6*7*8) - 4/(8*9*10) + 4/(10*11*12) - (4/(12*13*14) ...

needs math/big

with: n
: domul \ m -- 4/(m*m+1*m+2) m+2
	dup 1+ dup 1+ dup >r
	* * 4 swap / r> ;

: plus \ n m -- n+(4/m*m+1*m+2)  m+2
	domul >r + r> ;

: minus \ n m -- n+(4/m*m+1*m+2)  m+2
	domul >r - r> ;

var accuracy
var lastpi
var iterations
: done?
	lastpi @ - abs accuracy @ < ;

: nilakantha \ pi? n -- pi?1 
	\ are we there yet?
	over 
	done?  if drop ;then
	1 iterations +!
	\ sorry...
	over lastpi !
	plus minus recurse ;

: calcpi \ accuracy
	accuracy !
	3 bfloat 2 bfloat plus minus nilakantha ;

\ digits
9 dup  ##
dup 3 +  n#
"Calculating PI, may take a few minutes...\n" .
"0" swap n:1- s:* "0." swap s:+ "1" s:+ >n bfloat

calcpi 

"calculated in " . iterations @ .  " iterations" . cr
. cr
BIGPI . cr
accuracy @ .  cr
bye
